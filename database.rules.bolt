isAuthenticated() {
    return auth.uid !== null && auth.token.email !== null;
}

isAdmin(oid) {
    return root.security.organizations[oid].users[auth.uid] === 'admin';
}

isInNthDomain(organizationId, type, n) {
    return auth.token.email.endsWith('@' + root.security.organizations[organizationId].domains[type]['' + n]);
}

isInDomain(organizationId, type) {
    return isInNthDomain(organizationId, type, 0)
        || isInNthDomain(organizationId, type, 1)
        || isInNthDomain(organizationId, type, 2)
        || isInNthDomain(organizationId, type, 3)
        || isInNthDomain(organizationId, type, 4)
}

isAllowed(resource, privilege, organizationId) {
    return root.security.organizations[organizationId].permissions[
        root.security.organizations[organizationId].users[auth.uid] != null ?
            (root.security.organizations[organizationId].users[auth.uid] === 'admin' ?
                'member' :
                root.security.organizations[organizationId].users[auth.uid]
            ) :
            isInDomain(organizationId, 'member') ? 'member' : (isInDomain(organizationId, 'guest') ? 'guest' : 'anyone')
    ][resource][privilege]
}

isAllowedScope(scope, resource, privilege, oid) {
    return isAllowed((scope === 'organization' ? '' : 'personal_') + resource, privilege, oid) &&
            (scope === 'organization' || scope === auth.uid)
}

path /security/organizations/{oid} {
    read() { isAuthenticated() && isAdmin(oid) }
    write() { isAuthenticated() && isAdmin(oid) }
}

path /security/organizations/{oid}/permissions/{role} {
    read() { isAuthenticated() && (root.security.organizations[oid].users[auth.uid] === role || root.security.organizations[oid].users[auth.uid] == null && isInDomain(oid, role)) }
}

path /security/organizations/{oid}/users/{uid} {
    read() { isAuthenticated() && (uid === auth.uid || isAllowed('organization', 'read', oid) || isAdmin(oid)) }
    create() { isAuthenticated() && (uid === auth.uid && this === '?' || isAdmin(oid)) }
    update() { isAuthenticated() && isAdmin(oid) }
    delete() { isAuthenticated() && isAdmin(oid) }
}

path /flashlight/paths/{pid} {
    read() { false }
    write() {
        isAuthenticated() && (
            this != null && (
                isAdmin(this.index)
                ||
                pid.contains('-' + this.index + '-' + auth.uid + '-') && isAllowed(this.type, 'read', this.index)
            )
            ||
            this == null && (
                prior(this) == null
                ||
                isAdmin(prior(this.index))
                ||
                pid.contains('-' + prior(this.index) + '-' + auth.uid + '-')
            )
        )
    }
}

path /organizations/{oid} is Any {
    read() { isAuthenticated() && (isAllowed('organization', 'read', oid) || isAdmin(oid)) }
    write() { isAuthenticated() && isAdmin(oid) }
}

path /organizations/{oid}/users/{uid} {
    read() { isAuthenticated() && (uid === auth.uid || isAllowed('organization', 'read', oid) || isAdmin(oid)) }
    create() { isAuthenticated() && (uid === auth.uid) }
    update() { isAuthenticated() && (uid === auth.uid || isAdmin(oid)) }
    delete() { isAuthenticated() && isAdmin(oid) }
}

path /{branch}/organizations/{oid}/{scope}/{type} {
    read() { (branch === 'resources' || branch === 'trash') && isAuthenticated() && isAllowedScope(scope, type, 'read', oid) }
}

path /{branch}/organizations/{oid}/{scope}/{type}/{rid} {
    write() {
        (branch === 'resources' || branch === 'trash')
        && isAuthenticated()
        && isAllowedScope(scope, type, 'write', oid)
        && (
            prior(this) == null ||
            (scope === 'organization' && prior(this).creator === auth.uid || scope === auth.uid) ||
            this == null && isAdmin(oid)
        )
    }
}